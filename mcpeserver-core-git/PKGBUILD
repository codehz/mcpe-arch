pkgname=mcpeserver-core-git
pkgver=v0.1.6.r10.g22d4cc6
pkgrel=2
pkgdesc="Minecraft PE Server Core"
arch=('x86_64')
license=('GPL')
depends=(lib32-gcc-libs lib32-glibc lib32-gmp lib32-libffi lib32-libgcrypt lib32-libgpg-error lib32-libltdl lib32-libunistring lib32-systemd lib32-xz)
provides=('mcpeserver-core')
makedepends=('mcpe-sdk' 'nimake' 'gcc' 'cmake')
source=('mcpeserver-core-git::git+https://github.com/codehz/bedrockserver#branch=master')
md5sums=('SKIP')
url='https://github.com/codehz/bedrockserver'

pkgver() {
  cd "$pkgname"
  git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd "$srcdir/$pkgname"
  git submodule init
  git submodule update
}

build() {
  cd "$srcdir/$pkgname"
  PATH=/opt/mcpe-sdk/bin:$PATH nimake clean -v 5
  PATH=/opt/mcpe-sdk/bin:$PATH nimake build -v 5
}

package() {
  install -Dm0755 "$srcdir/$pkgname/bin/bedrockserver" "$pkgdir/opt/mcpeserver-core/bedrockserver"
  install -Dm0755 "$srcdir/$pkgname/bin/bridge.so" "$pkgdir/opt/mcpeserver-core/bridge.so"
}
